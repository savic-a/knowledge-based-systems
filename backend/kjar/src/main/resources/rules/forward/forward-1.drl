import com.ftn.sbnz.model.Client;
import com.ftn.sbnz.enumeration.Category
import com.ftn.sbnz.model.Transaction;
import com.ftn.sbnz.model.Warning;
import com.ftn.sbnz.model.Budget;
import java.util.Calendar;
import java.sql.Timestamp;
import java.util.List;


// 1. LEVEL

rule "Set Flag for 5 Purchases in a Category"
    when
        $client: Client($clientId: id)
        $category: Category() from Category.values() 
        $transactions: List(size >= 5) from accumulate(
            Transaction(clientId == $clientId, type == Transaction.Type.OUTCOME, category == $category, value > 0, date >= startOfMonth(), date <= today()), 
            collectList()
        )
        eval(!$client.hasFivePurchasesCategory($category))
    then
        modify($client) {
            addFivePurchasesCategory($category);
        }
        System.out.println("Category " + $category.name() + " added to FivePurchasesCategories for client " + $clientId);
end


rule "Set Flag for 3 Purchases in a Category"
    when
        $client: Client($clientId: id)
        $category: Category() from Category.values() 
        $transactions: List(size >= 3) from accumulate(
            Transaction(clientId == $clientId, type == Transaction.Type.OUTCOME, category == $category, value > 0, date >= startOfMonth(), date <= today()), 
            collectList()
        )
        eval(!$client.hasThreePurchasesCategory($category) && !$client.hasFivePurchasesCategory($category))
    then
        modify($client) {
            addThreePurchasesCategory($category);
        }
        System.out.println("Category " + $category.name() + " added to ThreePurchasesCategories for client " + $clientId);
end


// 2. LEVEL

rule "Set Flag3 if ThreePurchases and Spending > 10% Income" no-loop
    when
        $client: Client($clientId: id, flag3 == false)
        $category: Category() from $client.threePurchases
        $currentMonthIncome: Number(doubleValue > 0) from accumulate(
            $income: Transaction(clientId == $clientId, type == Transaction.Type.INCOME, date >= startOfMonth(), date <= today()), 
            sum($income.getValue())
        )
        $budget: Budget(clientId == $clientId)
        $totalSpending: Number(doubleValue > 0) from accumulate(
            $expense: Transaction(clientId == $clientId, type == Transaction.Type.OUTCOME, category == $category, value > 0, date >= startOfMonth(), date <= today()), 
            sum($expense.getValue())
        )
        eval($totalSpending.doubleValue() > (($currentMonthIncome.doubleValue() + $budget.getValue()) * 0.1))
    then
        modify($client) {
            setFlag3(true)
        }
        System.out.println("Flag3 set for client " + $clientId + " as spending exceeds 10% of income in category " + $category.name());
end

rule "Set Flag4 if FivePurchases and Spending > 25% Income" no-loop
    when
        $client: Client($clientId: id, flag4 == false)
        $category: Category() from $client.fivePurchases
        $currentMonthIncome: Number(doubleValue > 0) from accumulate(
            $income: Transaction(clientId == $clientId, type == Transaction.Type.INCOME, date >= startOfMonth(), date <= today()), 
            sum($income.getValue())
        )
        $budget: Budget(clientId == $clientId)
        $totalSpending: Number(doubleValue > 0) from accumulate(
            $expense: Transaction(clientId == $clientId, type == Transaction.Type.OUTCOME, category == $category, value > 0, date >= startOfMonth(), date <= today()), 
            sum($expense.getValue())
        )
        eval($totalSpending.doubleValue() > (($currentMonthIncome.doubleValue() + $budget.getValue()) * 0.25))
    then
        modify($client) {
            setFlag4(true)
        }
        System.out.println("Flag4 set for client " + $clientId + " as spending exceeds 25% of income in category " + $category.name());
end


// 3 LEVEL

rule "Generate Mild Warning for Flag3"
    when
        $client: Client($clientId: id, flag3 == true)
        $category: Category() from $client.threePurchases
    then
        insert(new Warning($clientId, "Mild", "You have spent more than 10% of your monthly income."));
        System.out.println("Mild warning generated for client " + $clientId + " in category " + $category.name());
end


rule "Generate High Warning for Flag4"
    when
        $client: Client($clientId: id, flag4 == true)
        $category: Category() from $client.fivePurchases
    then
        insert(new Warning($clientId, "High", "You have spent more than 25% of your monthly income."));
        System.out.println("High warning generated for client " + $clientId + " in category " + $category.name());
end


// Utility functions to get start of the current month and current day
function Timestamp startOfMonth() {
    Calendar cal = Calendar.getInstance();
    cal.set(Calendar.DAY_OF_MONTH, 1);
    cal.set(Calendar.HOUR_OF_DAY, 0);
    cal.set(Calendar.MINUTE, 0);
    cal.set(Calendar.SECOND, 0);
    cal.set(Calendar.MILLISECOND, 0);
    return new Timestamp(cal.getTimeInMillis());
}

function Timestamp today() {
    Calendar cal = Calendar.getInstance();
    cal.set(Calendar.HOUR_OF_DAY, 23);
    cal.set(Calendar.MINUTE, 59);
    cal.set(Calendar.SECOND, 59);
    cal.set(Calendar.MILLISECOND, 999);
    return new Timestamp(cal.getTimeInMillis());
}